#!/usr/bin/perl -l

use strict;
use warnings;

use LWP::Simple;
use XML::XPath;

die "Usage: $0 abbr [abbr]..." unless @ARGV;

my $url     = 'http://acronyms.silmaril.ie/cgi-bin/xaa?';
my $acro    = '/acronym/found/acro';
my $counter = 0;
foreach (@ARGV) {
    my $re = get("$url$_");
    next unless defined $re;
    my @acros = XML::XPath->new( xml => $re )->findnodes($acro);
    next unless @acros;
    print '' if $counter;
    print;
    map {
        my $node = $_;
        my @val  = map { $node->findvalue("$_/text()") } ( 'expan', 'comment' );
        my $def  = $val[0];
        $def .= " /* $val[1] */" if $val[1];
        print $def;
    } @acros;
    $counter++;
}
